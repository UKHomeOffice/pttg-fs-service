apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'groovy'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'application'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

plugins.apply(CheckstylePlugin)

buildscript {

    ext {
        springBootVersion = '1.4.1.RELEASE'
        springRestDocsVersion = '1.1.0.RELEASE'
        jarDocsDir = 'static'
        jarAppendix = 'docs'
        testSrcPattern = 'apidocs/**'
        sourceDocsDir = 'src/doc'
        jarDocsDir = 'static/docs'
        snippetsDir = 'build/generated-snippets'
        jarAppendix = 'docs'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url 'https://repo.spring.io/libs-milestone' }

    }
    dependencies {

        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
        classpath 'net.serenity-bdd:serenity-gradle-plugin:1.1.31'

        // This shouldn't be needed. It should pull transitively from the plugin. Don't know why it isn't working.
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
        classpath "org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11"
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'org.owasp:dependency-check-gradle:1.4.3'

    }
}

group 'com.hod'
version '0.1.0'

mainClassName = "uk.gov.digital.ho.proving.financialstatus.api.ServiceRunner"

setProperty('sourceCompatibility', '1.8')
setProperty('targetCompatibility', '1.8')

task('wrapper', type: Wrapper) {
    gradleVersion = '3.3'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

checkstyle {
    ignoreFailures = true
    config = project.resources.text.fromString("<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n" +
        "<!DOCTYPE module PUBLIC\n" +
        "    \"-//Puppy Crawl//DTD Check Configuration 1.3//EN\" \n" +
        "     \"http://www.puppycrawl.com/dtds/configuration_1_3.dtd\"> \n" +
        "<module name=\"Checker\">\n" +
        "  <module name=\"TreeWalker\">\n" +
        "    <module name=\"UnusedImports\"/> \n" +
        "  </module>\n" +
        "</module>")
}

checkstyleTest {
    group 'verification'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "http://repo.spring.io/plugins-release" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { // READ access
        name 'snapshots' // corresponds to entries in ~/.m2/settings.xml (required - see readme)
        url 'https://artifactory.digital.homeoffice.gov.uk/artifactory/libs-snapshot-local'
        credentials { // Used by Jenkins, overridden by your settings.xml
            username = System.env.ARTIFACTORY_USER
            password = System.env.ARTIFACTORY_PASS
        }
    }
}

dependencies {

    compile "org.codehaus.groovy:groovy-all:2.4.3"
    compile "org.json:json:20160212"

    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "ch.qos.logback:logback-core:1.1.3"
    compile "org.slf4j:slf4j-parent:1.7.14"

    compile "com.fasterxml.jackson.core:jackson-annotations:2.7.4"
    compile "com.fasterxml.jackson.core:jackson-databind:2.7.4"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.4"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.7.4"

    compile "com.sun.jersey:jersey-client:1.19"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:2.22.2"

    compile "org.springframework.boot:spring-boot:1.4.1.RELEASE"
    compile "org.springframework.boot:spring-boot-starter-web:1.4.1.RELEASE"

    compile "org.springframework.retry:spring-retry:1.1.4.RELEASE"
    compile "org.aspectj:aspectjrt:1.8.9"
    compile "org.aspectj:aspectjweaver:1.8.9"

    compile "org.springframework.boot:spring-boot-starter-actuator:1.4.1.RELEASE"

    compile 'com.fasterxml.jackson.module:jackson-module-scala_2.11:2.7.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.scala-lang:scala-library:2.11.8'

    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'

    compile "org.springframework.boot:spring-boot:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    
    compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '4.8'

    compile group: 'org.typelevel', name: 'cats_2.11', version: '0.9.0'


    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'com.jayway.jsonpath:json-path:2.2.0'
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19:"
    testCompile 'org.assertj:assertj-core:3.4.1'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.2'

    testCompile "org.springframework:spring-test:4.3.3.RELEASE"
    testCompile "org.springframework.boot:spring-boot-starter-test:1.4.1.RELEASE"


    testCompile "com.jayway.restassured:json-schema-validator:2.9.0"
    testCompile "com.jayway.restassured:rest-assured:2.9.0"

    testCompile "org.spockframework:spock-core:1.1-groovy-2.4-rc-3"
    testCompile "org.spockframework:spock-spring:1.1-groovy-2.4-rc-3"
    testCompile "cglib:cglib-nodep:3.2.1"
    testCompile "org.objenesis:objenesis:2.2"

    testCompile "info.cukes:cucumber-java:1.2.4"
    testCompile "info.cukes:cucumber-junit:1.2.4"
    testCompile "info.cukes:gherkin:2.12.2"
    testCompile "net.serenity-bdd:serenity-core:1.1.31"
    testCompile "net.serenity-bdd:serenity-cucumber:1.1.6"
    testCompile "net.serenity-bdd:serenity-junit:1.1.31"
    testCompile "net.serenity-bdd:serenity-spring:1.1.31"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
    testCompile "com.github.tomakehurst:wiremock:1.58"
    testCompile 'com.github.fakemongo:fongo:2.0.6'

    testCompile 'org.scala-lang:scala-library:2.11.8'

    testCompile "org.springframework.boot:spring-boot:$springBootVersion"
    testCompile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    testCompile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"

    testCompile "org.springframework.restdocs:spring-restdocs-core:$springRestDocsVersion"
    testCompile "org.springframework.restdocs:spring-restdocs-restassured:$springRestDocsVersion"

}

compileGroovy.dependsOn "compileScala"
compileTestGroovy.dependsOn "compileTestScala"


if (System.getenv('VERSION')) {
    version = System.getenv('VERSION')
}
if (System.getenv('BUILD_NUMBER')) {
    version = version + '-' + System.getenv('BUILD_NUMBER')
}
if (System.getenv('GIT_COMMIT')) {
    version = version + '.' + System.getenv('GIT_COMMIT')
}

task accept(type: Test) {
    include 'acceptance/**'
    finalizedBy aggregate
    outputs.upToDateWhen { false }
}

task acceptanceTest(type: Test) {
    description 'Runs the acceptance tests.'
    dependsOn 'accept'
    group 'verification'
    include 'acceptance/**'
    outputs.upToDateWhen { false }
}

tasks.withType(JavaCompile) {
    options.fork = true  // Fork your compilation into a child process
    options.forkOptions.setMemoryMaximumSize("256m")
}

tasks.withType(GroovyCompile) {
    options.fork = true  // Fork your compilation into a child process
    options.forkOptions.setMemoryMaximumSize("256m")
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.with {
        force = true
        additionalParameters = ["-Xmax-classfile-name", "127", "-feature", "-language:implicitConversions"]
        forkOptions.setMemoryMaximumSize("256m")
    }
}

test {
    exclude 'acceptance/**'
    maxHeapSize = "256m"
}

tasks.withType(Jar) {
    manifest {
        attributes(
            'Implementation-Title': "${jar.baseName}",
            'Implementation-Version': version
        )
    }
}

dependencyCheck {
    autoUpdate = false
    cveValidForHours = 1
    format = 'ALL'
    skipTestGroups = true
}

sourceSets {
    doc {
        resources.srcDir project.file("${sourceDocsDir}/resources")
    }
}

test {
    exclude "${testSrcPattern}"
}

task('apiDocTest', type: Test) {
    description 'Runs the API documentation tests (generates doc snippets to build/generated-snippets but does not process into HTML)'
    group 'verification'
    include "${testSrcPattern}"
    classpath = sourceSets.test.runtimeClasspath + project.files("${sourceDocsDir}/resources")
}

task('generateApiDocs', dependsOn: ['apiDocTest', 'asciidoctor']) {
    description 'Generates the API documentation - look in build/asciidoc for the output'
    group 'documentation'
}

asciidoctor {
    description 'You should use the generateApiDocs task instead'
    attributes 'snippets': project.file("${snippetsDir}")
    inputs.dir project.file("${snippetsDir}")
    outputDir 'build/asciidoc'
    sourceDir "${sourceDocsDir}/asciidoc"
    backends 'pdf', 'html5'
    mustRunAfter 'apiDocTest'

}


task('buildWithApiDocs', type: Jar, dependsOn: ['generateApiDocs', 'build']) {
    description 'Same as build but also generates API docs and adds them to the jar'
    group 'build'

    destinationDir = new File("build/libs/")

    project.afterEvaluate { // Wait for regular jar task to be evaluated first

        baseName = jar.baseName
        appendix = "${jarAppendix}"
        version = jar.version

        from zipTree(jar.archivePath)
        from("${asciidoctor.outputDir}/html5/index.html") { into "${jarDocsDir}" }
        from("${asciidoctor.outputDir}/pdf/index.pdf") { into "${jarDocsDir}" }
    }
}

task buildSpringBootWithApiDocs(type: BootRepackage, dependsOn: buildWithApiDocs) {
    group 'build'
    description 'Builds the jar as a Spring Boot executable jar containing the api docs'
}
